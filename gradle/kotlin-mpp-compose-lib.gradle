import static de.fayard.refreshVersions.core.Versions.versionFor
 /**
 * Plugin to create a kotlin MPP with safe defaults.
 */

apply plugin: "org.jetbrains.kotlin.multiplatform"
apply plugin: "com.android.library"
apply plugin: "org.jetbrains.compose"
apply from: "$rootDir/gradle/detekt.gradle"

android {
    compileSdkVersion 34
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packaging {
        resources {
            excludes += "/META-INF/**"
        }
    }

    kotlin {
        jvmToolchain(17)
    }

    sourceSets["main"].resources.srcDirs("src/commonMain/resources")

    buildFeatures {
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = versionFor(project, "version.androidx.compose.compiler")
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    implementation("androidx.activity:activity-compose:_")
    implementation("androidx.appcompat:appcompat:_")
    implementation("androidx.core:core-ktx:_")

    implementation("androidx.compose.ui:ui-tooling-preview:_")
    debugImplementation("androidx.compose.ui:ui-tooling:_")
    implementation("androidx.compose.foundation:foundation:_")
}

kotlin {
    targets.all {
        compilations.all {
        }
    }

    androidTarget() {
        compilations.all {
            kotlinOptions {

            }
        }
    }

    jvm() {
        compilations.all {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_17
            }
        }
    }

    // TODO: This target should be determined at configuration time since it depends on the
    // architecture of the host.
    iosSimulatorArm64() {

    }

    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":framework:interfacelib"))
                implementation(project(":framework:logging"))

                implementation(compose.runtime)
                implementation(compose.foundation)
                implementation(compose.material3)
                implementation(compose.materialIconsExtended)
                implementation(compose.components.resources)
                implementation(compose.animation)
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:_"
                implementation "org.jetbrains.kotlin:kotlin-test-common:_"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:_"
            }
        }
        androidMain {
        }
        androidUnitTest {
        }
        jvmMain {
            dependencies {
                implementation(compose.desktop.common)
                implementation("org.jetbrains.compose.ui:ui-tooling-preview:_")
                //implementation("androidx.compose.material3:material3:1.0.0-alpha14")
            }
        }
        jvmTest {
            dependencies {
                implementation("org.junit.jupiter:junit-jupiter-api:_")
                implementation("org.jetbrains.kotlin:kotlin-test-junit5:_")
                implementation("io.mockk:mockk:_")
            }
        }
        iosMain {
        }
        iosTest {
        }
    }
}

tasks {
    jvmTest {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

tasks.register("releaseJvm") {
    group = 'release'
    description = 'Run all the steps to build a releaseJvm artifact'
    dependsOn 'compileKotlinJvm'
    dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
    dependsOn 'detektJvmMain' // Run the code analyzer
    dependsOn 'jvmTest'
}

tasks.register("releaseAndroid") {
    group = 'release'
    description = 'Run all the steps to build a releaseAndroid artifact'
    dependsOn 'assembleDebug'
    dependsOn 'assembleRelease'
    dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
    dependsOn 'detektAndroidDebug' // Run the code analyzer
    dependsOn 'detektAndroidRelease' // Run the code analyzer
    dependsOn 'testDebugUnitTest'
    dependsOn 'testReleaseUnitTest'
}

tasks.register("releaseIos") {
    group = 'release'
    description = 'Run all the steps to build a releaseIos artifact'
    dependsOn 'compileKotlinIosSimulatorArm64'
    dependsOn 'detektIosSimulatorArm64Main' // Run the code analyzer on the common-code source set
    dependsOn 'detektIosSimulatorArm64Main' // Run the code analyzer
    dependsOn 'iosSimulatorArm64Test'
}

tasks.register("release") {
    group = 'release'
    description = 'Run all the steps to build a release artifact for all enabled targets'

    dependsOn 'releaseJvm'
    dependsOn 'releaseAndroid'
    dependsOn 'releaseIos'
}