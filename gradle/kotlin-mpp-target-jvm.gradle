/**
 * Plugin to create a kotlin JVM target with safe defaults.
 */
apply plugin: "org.jetbrains.kotlin.multiplatform"

kotlin {
    jvm() {
        compilations.all {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_21
            }
        }
    }

    sourceSets {
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:_"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:_"
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:_"
                implementation "org.junit.jupiter:junit-jupiter-api:_"
                implementation "org.junit.jupiter:junit-jupiter-params:_"
                implementation "org.jetbrains.kotlin:kotlin-test-junit5:_"
                implementation "io.mockk:mockk:_"

                runtimeOnly "org.junit.jupiter:junit-jupiter-engine:_"
            }
        }
    }
}

tasks {
    jvmTest {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            showStackTraces = true
            showCauses = true
            showExceptions = true
            exceptionFormat = 'full'
            events "passed", "skipped", "failed"
        }
    }
}

tasks.register("releaseJvm") {
    group = 'release'
    description = 'Run all the steps to build a releaseJvm artifact'
    dependsOn 'compileKotlinJvm'
    dependsOn 'detektCommonMainSourceSet' // Run the code analyzer on the common-code source set
    dependsOn 'detektJvmMainSourceSet' // Run the code analyzer
    dependsOn 'jvmTest'
}

release.dependsOn('releaseJvm')
