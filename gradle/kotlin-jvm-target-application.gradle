/**
 * Plugin to configure an application using kotlin-jvm.
 * This plugin expects that the project sets the value for mainClassTarget.
 * For example: ext.mainClassTarget = "com.cramsan.awslib.editor.LoginApp"
 */
apply from: "$rootDir/gradle/kotlin-jvm-target-lib.gradle" // Inherit the main configuration settings from the kotlin-jvm-lib plugin
apply plugin: "application" // Enable this application to run as an application.


if (!project.hasProperty('jarNameTarget') || jarNameTarget == null || jarNameTarget.toString().isBlank()) {
    throw new GradleException(
        "Missing required property 'jarNameTarget' in project '${project.name}'.\n" +
        "Please define it in your module's build.gradle file:\n\n" +
        "    ext.jarNameTarget = \"my-app\"\n" +
        "For a build.gradle.kts file, use:\n\n" +
        "    val jarNameTarget by extra(\"my-app\")\n"
    )
}

if (!project.hasProperty('mainClassTarget') || mainClassTarget == null || mainClassTarget.toString().isBlank()) {
    throw new GradleException(
        "Missing required property 'mainClassTarget' in project '${project.name}'.\n" +
        "Please define it in your module's build.gradle file:\n\n" +
        "    ext.mainClassTarget = \"com.example.MainKt\"\n" +
        "For a build.gradle.kts file, use:\n\n" +
        "    val mainClassTarget by extra(\"com.example.MainKt\")\n"
    )
}
// Export as a jar with the referenced main class to execute
jar {
    archiveBaseName.set(jarNameTarget)
    archiveVersion.set("")
    archiveClassifier.set("")
    manifest {
        attributes(
            'Main-Class' : mainClassTarget
        )
    }
}

// Setting the main class to run when executing as an application.
application {
    mainClass.set(mainClassTarget)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}
